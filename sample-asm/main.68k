;*******************************************************************************
; EntryPoint
; Where the program begins.
;*******************************************************************************

EntryPoint:
    move.w  #$2700, sr                  ; Disable interrupts
    
    moveq   #$0F, d0                    ; Deal with TMSS if needed
    and.b   (HwVersion), d0
    beq.s   @NoTmss
    move.l  #'SEGA', (TmssPort)
@NoTmss:
    
    bsr     InitJoypad                  ; Initialize joypad
    bsr     InitVideo                   ; Initialize video
    
    lea     (SoundData), a0             ; Initialize sound
    move.w  #SoundDataLen, d0
    bsr     MiniMusic_Init
    
    move.w  #$2000, sr                  ; Enable interrupts

;-------------------------------------------------------------------------------

MainLoop:
    bsr     ReadJoypad                  ; Read joypad buttons
    
    btst    #JOY_C, (JoyPress)          ; Press C to play a sound
    beq.s   MainLoop_NoPlay
    moveq   #1, d0
    bsr     MiniMusic_SendCmd
MainLoop_NoPlay:
    
    bsr     VSync                       ; Wait for next frame
    bra     MainLoop

;*******************************************************************************
; ErrorInt
; Handler for whenever the 68000 crashes. It just hangs up.
;*******************************************************************************

ErrorInt:
    move.w  #$2700, sr                  ; Disable interrupts
    bra.s   *                           ; Hang up forever
